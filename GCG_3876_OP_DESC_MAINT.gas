Program.Sub.ScreenSU.Start
gui.Form..create
gui.Form..caption("Outside Desc Maint.")
gui.Form..size(7035,5895)
gui.Form..minx(0)
gui.Form..miny(0)
gui.Form..position(0,0)
gui.Form..event(unload,form_unload)
gui.Form..alwaysontop(False)
gui.Form..fontname("Arial")
gui.Form..fontsize(8)
gui.Form..forecolor(0)
gui.Form..fontstyle(,,,,)
gui.Form..BackColor(-2147483633)
gui.Form..controlbox(True)
gui.Form..maxbutton(False)
gui.Form..minbutton(True)
gui.Form..mousepointer(0)
gui.Form..moveable(True)
gui.Form..sizeable(False)
gui.Form..ShowInTaskBar(True)
gui.Form..titlebar(True)
gui.Form.lbl1.create(label,"Outside Code",True,1935,255,1,200,200,True,0,Arial,8,-2147483633,0)
gui.Form.lbl1.defaultvalue("")
gui.Form.lbl1.controlgroup(0)
gui.Form.lbl2.create(label,"Description",True,1935,255,1,3300,245,True,0,Arial,8,-2147483633,0)
gui.Form.lbl2.defaultvalue("")
gui.Form.lbl2.controlgroup(0)
gui.Form.txtOP.create(textbox,"",True,2325,300,0,200,445,False,0,Arial,8,-2147483643,1)
gui.Form.txtOP.maxLength(20)
gui.Form.txtOP.defaultvalue("")
gui.Form.txtOP.controlgroup(0)
gui.Form.cmdOpBrowser.create(button)
gui.Form.cmdOpBrowser.caption("^")
gui.Form.cmdOpBrowser.visible(True)
gui.Form.cmdOpBrowser.size(465,375)
gui.Form.cmdOpBrowser.zorder(0)
gui.Form.cmdOpBrowser.position(2580,415)
gui.Form.cmdOpBrowser.enabled(True)
gui.Form.cmdOpBrowser.fontname("Arial")
gui.Form.cmdOpBrowser.fontsize(8)
gui.Form.cmdOpBrowser.event(click,cmdopbrowser_click)
gui.Form.cmdOpBrowser.defaultvalue("")
gui.Form.cmdOpBrowser.controlgroup(0)
gui.Form.txtDesc.create(textbox,"",True,3150,300,0,3305,450,True,0,Arial,8,-2147483643,1)
gui.Form.txtDesc.event(gotfocus,txtdesc_gotfocus)
gui.Form.txtDesc.defaultvalue("")
gui.Form.txtDesc.controlgroup(0)
gui.Form.cmdSave.create(button)
gui.Form.cmdSave.caption("Save")
gui.Form.cmdSave.visible(True)
gui.Form.cmdSave.size(855,345)
gui.Form.cmdSave.zorder(0)
gui.Form.cmdSave.position(200,890)
gui.Form.cmdSave.enabled(True)
gui.Form.cmdSave.fontname("Arial")
gui.Form.cmdSave.fontsize(8)
gui.Form.cmdSave.event(click,cmdSave_click)
gui.Form.cmdSave.defaultvalue("")
gui.Form.cmdSave.controlgroup(0)
gui.Form.cmdDelete.create(button)
gui.Form.cmdDelete.caption("Delete")
gui.Form.cmdDelete.visible(True)
gui.Form.cmdDelete.size(855,345)
gui.Form.cmdDelete.zorder(0)
gui.Form.cmdDelete.position(1120,890)
gui.Form.cmdDelete.enabled(True)
gui.Form.cmdDelete.fontname("Arial")
gui.Form.cmdDelete.fontsize(8)
gui.Form.cmdDelete.event(click,cmddelete_click)
gui.Form.cmdDelete.defaultvalue("")
gui.Form.cmdDelete.controlgroup(0)
gui.Form.gsfgGrid.create(gsflexgrid)
gui.Form.gsfgGrid.FixedRows(0)
gui.Form.gsfgGrid.FixedCols(0)
gui.Form.gsfgGrid.visible(True)
gui.Form.gsfgGrid.size(6315,3915)
gui.Form.gsfgGrid.zorder(0)
gui.Form.gsfgGrid.position(200,1290)
gui.Form.gsfgGrid.enabled(True)
gui.Form.gsfgGrid.event(commandclick,gsfggrid_commandclick)


Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start

Variable.UDT.uGrid.Define("OP_CODE",String,OP_CODE)
Variable.UDT.uGrid.Define("EXCEL_DESC",String,EXCEL_DESC)
Variable.uGlobal.uGrid.Declare("uGrid")
Program.Sub.Preflight.End

Program.Sub.cmdOpBrowser_Click.Start
F.Intrinsic.Control.SetErrorHandler("Preflight_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
V.Local.sTitle.Declare(String)
V.Local.iWidth.Declare(Long)
V.Local.sRet.Declare(String)

'Show them the OP Code Browser for Outside Operations only?
'OP_CODES table Machine is Op CODE and Operation is the Vendor
'FOr outside that's the code we want. WE WANT MACHINE
'CAUSE IN THE SCRIPT OR EXCEL WE ARE GOING AGAINST OUTSIDE CODES.

'set titles
F.Intrinsic.String.Split("Outside Code*!*Vendor*!*Description","*!*",v.Local.sTitle)
F.Intrinsic.String.Split("2000*!*1000*!*3000","*!*",v.Local.iWidth)

F.Intrinsic.UI.SetBrowserHotTypeAhead(True)
F.Intrinsic.UI.Browser("Select OP Code","conX","Select Machine,Operation,Description From V_OP_CODES Where LMO = 'O' Order By Machine",v.Local.sTitle,v.Local.iWidth,2000,10500,v.Local.sRet)

F.Intrinsic.Control.If(v.Local.sRet.Trim,<>,"***CANCEL***")
	F.Intrinsic.String.Split(v.Local.sRet,"*!*",v.Local.sRet)
	Function.Intrinsic.Control.BlockEvents
	Gui.Form.txtOP.Text(v.Local.sRet(0).Trim)
	'find desc from custom table
	Function.Intrinsic.Control.CallSub(Get_desc)
	Function.Intrinsic.Control.UnBlockEvents
F.Intrinsic.Control.EndIf

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("Preflight_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GCG_3876_OP_DESC_MAINT.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	Function.Intrinsic.Control.CallSub(Form_unload)
Function.Intrinsic.Control.EndIf


Program.Sub.cmdOpBrowser_Click.End

Program.Sub.cmdSave_Click.Start
F.Intrinsic.Control.SetErrorHandler("cmdOpBrowser_Click_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
V.Local.sSQL.Declare(String)
V.Local.sOP.Declare(String)

V.Local.sOP.Set(V.Screen.Form!txtOP.Text)

F.Intrinsic.Control.If(V.Local.sOP.Trim,=,"")
	F.Intrinsic.UI.Msgbox("OP Code cannot be blank. Verify input.","Error...")
	F.Intrinsic.Control.ExitSub
Function.Intrinsic.Control.EndIf


Function.Intrinsic.Control.BlockEvents

F.Intrinsic.String.Build("Select * From GCG_3876_OP_DESC Where OP_CODE = '{0}'",V.Local.sOP.Trim,V.Local.sSQL)

F.ODBC.Connection!conX.OpenLocalRecordsetRW("rstSave",V.Local.sSQL)

F.Intrinsic.Control.If(V.ODBC.conX!rstSave.EOF,=,True)
	F.ODBC.conX!rstSave.AddNew
	F.ODBC.conX!rstSave.Set!OP_CODE(V.Local.sOP.Trim)
Function.Intrinsic.Control.EndIf
F.ODBC.conX!rstSave.Set!Excel_Desc(V.Screen.Form!txtDesc.Text)

F.ODBC.conX!rstSave.Update

F.ODBC.conX!rstSave.Close

F.Intrinsic.Control.CallSub(Load_grid)


Function.Intrinsic.Control.UnBlockEvents

'clear out the forms. set focus back to the browser
Gui.Form.txtDesc.Text("")
Gui.Form.txtOP.Text("")
Gui.Form.cmdOpBrowser.SetFocus

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("cmdOpBrowser_Click_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GCG_3876_OP_DESC_MAINT.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	Function.Intrinsic.Control.CallSub(Form_unload)
Function.Intrinsic.Control.EndIf


Program.Sub.cmdSave_Click.End

Program.Sub.Form_UnLoad.Start
F.Intrinsic.Control.SetErrorHandler("txtDesc_GotFocus_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)


F.ODBC.Connection!conX.Close
F.Intrinsic.Control.End

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("txtDesc_GotFocus_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GCG_3876_OP_DESC_MAINT.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	Function.Intrinsic.Control.End

Function.Intrinsic.Control.EndIf


Program.Sub.Form_UnLoad.End

Program.Sub.Main.Start
F.Intrinsic.Control.SetErrorHandler("Preflight_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)

F.ODBC.Connection!conX.OpenConnection(V.Ambient.PDSN,V.Ambient.PUser,V.Ambient.PPass)

F.Intrinsic.Control.CallSub(Buildstyle)
F.Intrinsic.Control.CallSub(Load_grid)

Gui.Form..Show
Gui.Form.cmdOpBrowser.SetFocus


F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("Preflight_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GCG_3876_OP_DESC_MAINT.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	Function.Intrinsic.Control.CallSub(Form_unload)
Function.Intrinsic.Control.EndIf


Program.Sub.Main.End

Program.Sub.txtDesc_GotFocus.Start
F.Intrinsic.Control.SetErrorHandler("Main_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)


'selete all text in textbxo
gui.Form.txtDesc.SelectAll


F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("Main_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GCG_3876_OP_DESC_MAINT.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	Function.Intrinsic.Control.CallSub(form_unload)
Function.Intrinsic.Control.EndIf


Program.Sub.txtDesc_GotFocus.End

Program.Sub.Get_Desc.Start
F.Intrinsic.Control.SetErrorHandler("Get_Desc_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
v.Local.sSQL.Declare(String)
v.Local.sOP.Declare(String)

v.Local.sOP.Set(V.Screen.Form!txtOP.Text)

f.Intrinsic.Control.If(V.Local.sOP.Trim,=,"")
	f.Intrinsic.Control.ExitSub
Function.Intrinsic.Control.EndIf

f.Intrinsic.String.Build("Select Excel_Desc From GCG_3876_OP_Desc Where OP_CODE = '{0}'",V.Local.sOP.Trim,V.Local.sSQL)
f.ODBC.Connection!conX.OpenLocalRecordsetRO("rst",V.Local.sSQL)

F.Intrinsic.Control.If(V.ODBC.conX!rst.EOF,<>,True)
	gui.Form.txtDesc.Text(V.ODBC.conX!rst.FieldValTrim!Excel_Desc)
	gui.Form.txtDesc.SetFocus
Function.Intrinsic.Control.Else
	gui.Form.txtDesc.Text("")
Function.Intrinsic.Control.EndIf

f.ODBC.conX!rst.Close



F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("Get_Desc_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GCG_3876_OP_DESC_MAINT.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
Function.Intrinsic.Control.EndIf


Program.Sub.Get_Desc.End

program.sub.cmddelete_click.start
F.Intrinsic.Control.SetErrorHandler("cmddelete_click_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
V.Local.sOp.Declare(String)
V.Local.sSQL.Declare(String)



V.Local.sOP.Set(V.Screen.Form!txtOP.Text)

F.Intrinsic.Control.If(V.Local.sOP.Trim,=,"")
	F.Intrinsic.Control.ExitSub
Function.Intrinsic.Control.EndIf

Function.Intrinsic.Control.BlockEvents

F.Intrinsic.String.Build("Delete From GCG_3876_OP_DESC Where OP_CODE = '{0}'",V.Local.sOP.Trim,V.Local.sSQL)

F.ODBC.Connection!conX.Execute(V.Local.sSQL)

Gui.Form.txtDesc.Text("")
Gui.Form.txtOP.Text("")

'load grid again
F.Intrinsic.Control.CallSub(Load_grid)

Function.Intrinsic.Control.UnBlockEvents

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("cmddelete_click_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GCG_3876_OP_DESC_MAINT.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	F.Intrinsic.Control.CallSub(Form_unload)

Function.Intrinsic.Control.EndIf


program.sub.cmddelete_click.End

program.sub.gsfggrid_commandclick.start
F.Intrinsic.Control.SetErrorHandler("gsfggrid_commandclick_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
V.Local.sRow.Declare(String)


'read row and set to our texboxes.
F.Intrinsic.Control.If(V.Args.key,>,0)
	Gui.Form.gsfgGrid.ReadRow(V.Args.key,V.Local.sRow)
	F.Intrinsic.Control.If(V.Local.sRow.Trim,<>,"")
		F.Intrinsic.String.Split(V.Local.sRow,"*!*",V.Local.sRow)
		Gui.Form.txtOP.Text(V.Local.sRow(0).Trim)
		Gui.Form.txtDesc.Text(V.Local.sRow(1).Trim)

	F.Intrinsic.Control.EndIf
	
F.Intrinsic.Control.EndIf



F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("gsfggrid_commandclick_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GCG_3876_OP_DESC_MAINT.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	F.Intrinsic.Control.CallSub(Form_unload)
Function.Intrinsic.Control.EndIf


program.sub.gsfggrid_commandclick.End

Program.Sub.BuildStyle.Start
F.Intrinsic.Control.SetErrorHandler("BuildStyle_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
V.Local.ic.Declare(Long)

Gui.Form.gsfgGrid.Rows(1)
Gui.Form.gsfgGrid.Rows(2)
Gui.Form.gsfgGrid.FixedRows(1)
Gui.Form.gsfgGrid.Cols(3)

Gui.Form.gsfgGrid.SetColumnPercentages(".30:.52:.16")


'open up texts
Gui.Form.gsfgGrid.TextMatrix(0,0,"Outside Code")
Gui.Form.gsfgGrid.TextMatrix(1,0,"Excel Desc.")


'Column1
Gui.Form.gsfgGrid.BuildStyle(1,0,"Format","Alignment",4)
Gui.Form.gsfgGrid.BuildStyle(2,0,"Format","Alignment",2)
Gui.Form.gsfgGrid.BuildStyle(2,0,"Type","Locked",True)


'Column 2
Gui.Form.gsfgGrid.BuildStyle(1,1,"Format","Alignment",4)
Gui.Form.gsfgGrid.BuildStyle(2,1,"Format","Alignment",2)
Gui.Form.gsfgGrid.BuildStyle(2,1,"Type","Locked",True)

'Column 3
Gui.Form.gsfgGrid.BuildStyle(1,2,"Format","Alignment",4)
Gui.Form.gsfgGrid.BuildStyle(2,2,"Type","Command","Edit")



F.Intrinsic.Control.ExitSub


F.Intrinsic.Control.Label("BuildStyle_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GCG_3876_OP_DESC_MAINT.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	F.Intrinsic.Control.CallSub(Form_unload)
Function.Intrinsic.Control.EndIf


Program.Sub.BuildStyle.End

Program.Sub.Load_Grid.Start
F.Intrinsic.Control.SetErrorHandler("Load_Grid_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)

Gui.Form.gsfgGrid.Visible(False)

F.ODBC.Connection!conX.OpenLocalRecordsetRO("rst","Select * From GCG_3876_OP_DESC ORDER BY OP_CODE")

F.Intrinsic.Control.If(V.ODBC.conX!rst.EOF,=,False)
	F.Intrinsic.Variable.LoadUDTFromRecordset("conx","rst","v.uglobal.uGrid",False)
	Gui.Form.gsfgGrid.LoadFromUDT("v.uglobal.uGrid","OP_CODE*!*EXCEL_DESC",2)

F.Intrinsic.Control.Else
	F.Intrinsic.Control.CallSub(Buildstyle)
F.Intrinsic.Control.endif

F.ODBC.conX!rst.Close

Gui.Form.gsfgGrid.Visible(True)


F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("Load_Grid_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GCG_3876_OP_DESC_MAINT.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	F.Intrinsic.Control.CallSub(Form_unload)
Function.Intrinsic.Control.EndIf


Program.Sub.Load_Grid.End

Program.Sub.Comments.Start
${$0$}$$}$TRT$}$05/24/2016 2:01:23 PM$}$False
${$3$}$0$}$$}$0$}$-1$}$$}$12:00:00 AM$}$<b>Maintenance Program</b>
Custom Table: GCG3876OP_DESC
OP_CODE(char 6) Primary Key,
EXCEL_DESC(CHAR 30)

Program will have a screen that the user can select from existing op-codes in GSS, and assign the matching description that will be in the spreadsheets that are read. These values will be saved to a table that the gab program will read from to determine which OP Code to use based on the description in the excel sheet for outside operations(i.e. "STRESS FAB" = "STR1" for example.)

Screen will have standard Op code browser that will look in Op_codes table and load OPERATION, DESCRIPTION to two textboxes. Description is the only one that is editable.

A grid will be below the textboxes that shows existing records in the custom table, 2 columns, Op Code, Excel Description.

Save Button will save the op_code and Description to the custom table, Op Code is the Primary Key.

Program.Sub.Comments.End

